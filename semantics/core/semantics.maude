--- basic arithmetic and logical semantics
fmod ALU-SEMANTICS is
  inc CODE-CONFIG .
 
  var SV1 : String .
  var K : K .
  vars T1 T2 : Rat .
  vars ENUMTYPE ENUMVAL : Pid .
  vars V1 V2 : SemanticValue .
  var BITFIELD : Pid .
  var BITINDEX : Nat .

  eq k(# T1  ~> K) = k(syn2sem(# T1) ~> K) .
  eq k(# SV1  ~> K) = k(syn2sem(# SV1) ~> K) .
  eq k(TRUE  ~> K) = k(syn2sem(TRUE) ~> K) .
  eq k(FALSE   ~> K) = k(syn2sem(FALSE) ~> K) .
  eq k(T# T1 ms  ~> K) = k(syn2sem(T# T1 ms) ~> K) .
  eq k(ENUMTYPE # ENUMVAL  ~> K) = k([enumVal(ENUMTYPE, ENUMVAL)] ~> K) .

  eq k(rcvError ~> K) = k(syn2sem(rcvError) ~> K) . 

  ---bitfield
  eq k(V1 . # T1  ~> K) = k(V1 . syn2sem(# T1) ~> K) .
endfm


mod ASSIGNMENT-LOOKUP-SEMANTICS is
  inc CODE-CONFIG .
  inc CONVERSION .
  inc QID .
  inc COMMON-SEMANTIC-VALUE .
  inc CONVERSION-SEMANTICVAL .

  var INSLOC : ProcessId .
  var I : Pid .
  vars V VV : SemanticValue .
  var K : K .
  vars ENV INSENV  : Env .
  var ENVSTACK : ProgramStack .
  vars ARRAYENV ARRAYENV1 ARRAYENV2  : ArrayEnv .
  var STORE : Map{Location, SemanticValue} .
  vars CONST CONST' : SemanticValue .
  var ARRAYINDEX  LOCINDEX : Nat .
  vars RESTINSS INSS : KConfig .
  var LOCATION  : Location .
  vars REST : KConfig .  
  var TYPE : PType .
  var VS : VSet .
  var LOG : Log .
  var RA : Qid .

--- assignment

  op @store : Location SemanticValue -> KLabel [ctor] .
 ceq k(@store(LOCATION,V) ~> K) store(STORE) 
   = k(K) store(insert(LOCATION, conversion0and1toBOOL(TYPE,V), STORE))
  if loc(LOCINDEX, TYPE) := LOCATION /\ $hasMapping(STORE,LOCATION) .

  --- global
  rl [globalAssign] :
     log(LOG)
     k(I := V ~> K) callStack(ENV :: ENVSTACK) globalVars(@(I) VS)
  => log(LOG (I , 'globalAssign, V))
     k(@store(ENV[I],V) ~> K) callStack(ENV :: ENVSTACK) globalVars(@(I) VS) .

 crl [localAssign] : k(I := V ~> K) callStack(ENV :: ENVSTACK) globalVars(VS)
  => k(@store(ENV[I],V) ~> K) callStack(ENV :: ENVSTACK) globalVars(VS)
  if not in(I, VS) .

---  eq task(k(InsLoc(INSLOC) . I := V ~> K) inss(INSS) 
---   = task(k(@store(getInsEnv(INSLOC,INSS)[I],V) ~> K) inss(INSS) .

 crl [arrayAssign] : k(arrayLoc(ARRAYENV)[VV] := V ~> K) constraints(CONST)
  => k(@store(LOCATION,V) ~> K) constraints(simplify(CONST AND CONST'))
  if arrayLookup(ARRAYENV, VV, CONST) => {LOCATION, CONST'} .

--- lookup --- get location
  op @lookup : Location -> KLabel [ctor] .
  eq k(@lookup(LOCATION) ~> K) store(STORE) 
   = k(STORE[LOCATION] ~> K) store(STORE) .

  --- Pid
  --- global
  rl [globalLookup] :
     log(LOG) store(STORE)
     k(I ~> K) callStack(ENV :: ENVSTACK) globalVars(@(I) VS) 
  => log(LOG (I , 'globalLookUp, placeHolder)) store(STORE)
     k(STORE[ENV[I]] ~> K) callStack(ENV :: ENVSTACK) globalVars(@(I) VS) .

  crl [localLookup] : k(I ~> K) callStack(ENV :: ENVSTACK) globalVars(VS)
   => k(@lookup(ENV[I]) ~> K) callStack(ENV :: ENVSTACK) globalVars(VS)
   if not in(I, VS) .
  --- _._._._
  eq k(InsLoc(INSLOC) . I ~> K)                 inss(INSS) 
   = k(@lookup(getInsEnv(INSLOC,INSS)[I]) ~> K) inss(INSS) .

  --- array[V]
 crl [arrayLookup] : k(arrayLoc(ARRAYENV)[VV] ~> K) constraints(CONST)
  => k(@lookup(LOCATION) ~> K) constraints(simplify(CONST AND CONST'))
  if arrayLookup(ARRAYENV, VV, CONST) => {LOCATION, CONST'} .

 crl [array]: arrayLookup((ARRAYENV1, (ARRAYINDEX, LOCATION), ARRAYENV2), V, CONST) 
           => {LOCATION, (V ==== syn2sem(# ARRAYINDEX))}
  if checkSAT(CONST AND (V ==== syn2sem(# ARRAYINDEX))) .
endm

--- conditional statement semantics
mod CONDITIONAL-SEMANTICS is 
  inc CODE-CONFIG .
  inc COMMON-SEMANTIC-VALUE .

  vars I : Pid .
  vars V IFSTATEMENT ITECONDITION : SemanticValue .
  vars PV PV1 PV2 : PValue .
  var K : K .
  var BB : BBranch .
  vars S1 S2 : Stmt .
  var CB : CBranch .
  var CONST : SemanticValue .
  vars IFSTATEMENT2 WHILESTATEMENT REPEATSTATEMENT PE1 PE2 : PExp .
  var REST : KConfig .  
  var LOG : Log .
  var RA : Qid .

  --- ite
 crl [iteTrue] : (k(ite(ITECONDITION, PE1, PE2) ~> K)) constraints(CONST) 
 => (k(PE1 ~> K))  
    constraints(CONST AND (ITECONDITION))
    if checkSAT(CONST AND (ITECONDITION)) .
  
 crl [iteFalse] : (k(ite(ITECONDITION, PE1, PE2) ~> K)) constraints(CONST) 
 => (k(PE2 ~> K)) 
    constraints(CONST AND (NOT ITECONDITION)) 
    if checkSAT(CONST AND (NOT ITECONDITION)) .

 --- if statement
 crl [IFTRUE] :  
     log(LOG) 
     k(IF IFSTATEMENT THEN S1 ; BB ENDIF ~> K) 
     constraints(CONST)
  => log(LOG (' , 'IFTRUE, placeHolder)) 
     k(S1 ~> K)  
     constraints(simplify(CONST AND IFSTATEMENT))
  if checkSAT(CONST AND IFSTATEMENT) .

 crl [IFFALSE] : 
     log(LOG) 
     k(IF IFSTATEMENT THEN S1 ; emptyBBranch ENDIF ~> K)  
     constraints(CONST)
  => log(LOG (' , 'IFFALSE, placeHolder)) 
     k(K)  
     constraints(simplify(CONST AND (NOT IFSTATEMENT)))
  if checkSAT(CONST AND (NOT IFSTATEMENT)) .

 crl [IFFALSE-ELSIF] : 
     log(LOG) 
     k(IF IFSTATEMENT THEN S1 ; ELSIF IFSTATEMENT2 THEN S2 ; BB ENDIF ~> K)  
     constraints(CONST)
  => log(LOG (' , 'IFFALSE-ELSIF, placeHolder)) 
     k(IF IFSTATEMENT2 THEN S2 ; BB ENDIF ~> K)  
     constraints(simplify(CONST AND (NOT IFSTATEMENT)))
  if checkSAT(CONST AND (NOT IFSTATEMENT)) .

 crl [IFFALSE-ELSE] : 
     log(LOG)  
     k(IF IFSTATEMENT THEN S1 ; ELSE S2 ; ENDIF ~> K)  
     constraints(CONST)
  => log(LOG (' , 'IFFALSE-ELSE, placeHolder)) 
     k(S2 ~> K)  
     constraints(simplify(CONST AND (NOT IFSTATEMENT)))
  if checkSAT(CONST AND (NOT IFSTATEMENT)) .


  --- case statement
  eq  k(CASE V OF emptyCBranch ELSE S1 ENDCASE ~> K)  
   =  k(S1 ~> K)   .

 crl [caseMatch] :  
     log(LOG) 
     k(CASE (V)  OF (@ PV2 : S1) CB ELSE S2 ENDCASE ~> K)   
     constraints(CONST)
  => log(LOG (' , 'caseMatch, V)) 
     k(S1 ~> K)   
     constraints(simplify(CONST AND V ==== syn2sem(PV2)))
  if checkSAT(CONST AND V ==== syn2sem(PV2)) .
  

 crl [caseMatchRanged] :  
     k(CASE (V)  OF (@ PV1 .. PV2 : S1) CB ELSE S2 ENDCASE ~> K)   
     constraints(CONST) 
  => k(S1 ~> K)    
     constraints(simplify(CONST AND syn2sem(PV1) <= V  AND V <= syn2sem(PV2)))
  if checkSAT(CONST AND syn2sem(PV1) <= V  AND V <= syn2sem(PV2)) .

 crl [caseNotMatch] :  
     k(CASE (V)  OF (@ PV2 : S1) CB ELSE S2 ENDCASE ~> K)   
     constraints(CONST) 
  => k(CASE (V)  OF CB ELSE S2 ENDCASE ~> K)   
     constraints(simplify(CONST AND V <> syn2sem(PV2))) 
  if checkSAT(CONST AND V <> syn2sem(PV2)) .

 crl [caseNotMatchRanged] :  
     k(CASE (V)  OF (@ PV1 .. PV2 : S1) CB ELSE S2 ENDCASE ~> K)   
     constraints(CONST) 
  => k(CASE (V)  OF CB ELSE S2 ENDCASE ~> K)    
     constraints(simplify(CONST AND (syn2sem(PV1) > V  OR V > syn2sem(PV2))))
  if checkSAT(CONST AND (syn2sem(PV1) > V  OR V > syn2sem(PV2))) .

  --- while statement
  eq k(WHILE WHILESTATEMENT DO S1 ENDWHILE ~> K)  
   = k(IF WHILESTATEMENT THEN S1 ; WHILE WHILESTATEMENT DO S1 ENDWHILE ; ENDIF ~> K)   .

  --- repeat statement
  eq k(REPEAT S1 UNTIL REPEATSTATEMENT ENDREPEAT ~> K)  
   = k(IF NOT REPEATSTATEMENT THEN S1 ; REPEAT S1 UNTIL REPEATSTATEMENT ENDREPEAT ; ENDIF ~> K)   .

  --- for statement
  eq k(FOR I := V TO PV1 BY PV2 DO S1 ENDFOR ~> K)   
   = k(I := V ~> WHILE I <= PV1 DO S1 ; I := I + PV2 ; ENDWHILE ~> K)   .
endm


--- calling of function block, and subprogram
mod CALL-SEMANTICS is
  inc CODE-CONFIG .
  inc COMMON-SEMANTIC-VALUE .

  var CPR : ProcessId .
  vars ENV ENV' INSENV  : Env .
  vars REST RESTINSS INSS : KConfig .  
  var K : K .
  var S : Stmt .
  var ARGS : List{FArgElement} .
  vars STORE STORE' : Map{Location, SemanticValue} .
  var LOCATION : Location .
  vars FBI FBNAME I1 I2 : Pid .
  vars INSLOC ARRAYINDEX : Nat .
  vars V1 V2 CONST CONST' : SemanticValue .
  var RESTENV : ProgramStack .
  var RESTID : List{ProcessId} .
  var PARMETERDATA : VarSecTypePairList .
  vars PARMETERDATA1 PARMETERDATA2 : VarSecTypePairList .
  vars ONEARG PE1 : PExp .
  var P1 : PProperExp  .
  vars ARRAYENV : ArrayEnv .
  var RA : Qid .
  
--- udfb call
  op @call : ProcessId List{FArgElement} -> KLabel [ctor] .
  op @call : ProcessId List{FArgElement} Env Pid -> KLabel [ctor] .
  op @args : List{FArgElement} -> KLabel [ctor] .
  op @bind : Env ProcessId -> KLabel [ctor] .
  op @pop : -> KLabel [ctor] .
  op tag : List{FArgElement} VarSecTypePairList -> List{FArgElement} .

 crl [fbcall] : k(FBI(ARGS) ~> K) callStack(ENV :: RESTENV)  store(STORE) 
  => k(@call(INSLOC,ARGS) ~> K) callStack(ENV :: RESTENV)  store(STORE) 
  if InsLoc(INSLOC) := STORE[ENV[FBI]] .

 ceq k(@call(INSLOC,ARGS) ~> K)  inss(INSS)  
   = k(@call(INSLOC,ARGS,INSENV,FBNAME) ~> K)  inss(INSS) 
  if INSENV := getInsEnv(INSLOC,INSS)
  /\ FBNAME := getInsType(INSLOC,INSS) .

  eq k(@call(INSLOC,ARGS,INSENV,FBNAME) ~> K)  
     PARDef(FBNAME, PARMETERDATA, S) 
   = k(@args(tag(ARGS,PARMETERDATA)) ~> @bind(INSENV,INSLOC) ~> S ~> @pop ~> K)  
     PARDef(FBNAME, PARMETERDATA, S) .

  op @ALTERNATIVE : -> PExp [ctor] .
  op INPAR(_) : Pid -> Pid [ctor] .
  op INOUTPAR(_) : Pid -> Pid [ctor] .

  eq tag((ONEARG, ARGS) , ((I1, ##IN)    PARMETERDATA)) = (INPAR(I1)    <:= ONEARG), tag(ARGS, PARMETERDATA) .
  eq tag((ONEARG, ARGS) , ((I1, ##INOUT) PARMETERDATA)) = (INOUTPAR(I1) <:= ONEARG), tag(ARGS, PARMETERDATA) .
  eq tag(nilPExpList, emptyVarSecTypePairList) = nilPExpList .

  eq tag(((I1 <:= ONEARG) , ARGS) , (PARMETERDATA1 (I1, ##IN)    PARMETERDATA2)) = (INPAR(I1)    <:= ONEARG), tag(ARGS, (PARMETERDATA1 PARMETERDATA2)) .
  eq tag(((I1 <:= ONEARG) , ARGS) , (PARMETERDATA1 (I1, ##INOUT) PARMETERDATA2)) = (INOUTPAR(I1) <:= ONEARG), tag(ARGS, (PARMETERDATA1 PARMETERDATA2)) .
  eq tag(nilPExpList, emptyVarSecTypePairList) = nilPExpList .

  --- parameter computation
  --- in parameter
  eq k(@args(((INPAR(I1) <:= P1) , ARGS))  ~> K) = k(P1 ~> @args(((INPAR(I1) <:= @ALTERNATIVE) , ARGS))  ~> K) .
  eq k(V1 ~> @args(((INPAR(I1) <:= @ALTERNATIVE) , ARGS))  ~> K) = k(@args(((INPAR(I1) <:= V1) , ARGS))  ~> K) .

  --- inout parameter
  eq k(@args(((INOUTPAR(I1) <:= P1 . I2) , ARGS))  ~> K) = k(@args(P1 ~> ((INOUTPAR(I1) <:= @ALTERNATIVE . I2) , ARGS))  ~> K) .
  eq k(@args(V1 ~> ((INOUTPAR(I1) <:= @ALTERNATIVE . I2) , ARGS))  ~> K) = k(@args(((INOUTPAR(I1) <:= V1 . I2) , ARGS))  ~> K) .

  eq k(@args(((INOUTPAR(I1) <:= P1[PE1]) , ARGS))  ~> K) = k(@args(P1 ~> ((INOUTPAR(I1) <:= @ALTERNATIVE[PE1]) , ARGS))  ~> K) .
  eq k(@args(V1 ~> ((INOUTPAR(I1) <:= @ALTERNATIVE[PE1]) , ARGS))  ~> K) = k(@args(V1 ~> ((INOUTPAR(I1) <:= V1[PE1]) , ARGS))  ~> K) .

  eq k(@args(((INOUTPAR(I1) <:= V1[P1]) , ARGS))  ~> K) = k(@args(P1 ~> ((INOUTPAR(I1) <:= V1[@ALTERNATIVE]) , ARGS))  ~> K) .
  eq k(@args(V2 ~> ((INOUTPAR(I1) <:= V1[@ALTERNATIVE]) , ARGS))  ~> K) = k(@args(((INOUTPAR(I1) <:= V1[V2]) , ARGS))  ~> K) .


  --- parameter binding

  ---in parameter
  eq k(@args((INPAR(I1) <:= V1) , ARGS) ~> @bind(ENV,CPR)  ~> K) 
   = k(@args(ARGS) ~> @bind(ENV,CPR) ~> I1 := V1  ~> K)  .

  --- inout parameter
  eq (k(@args((INOUTPAR(I1) <:= I2) , ARGS) ~> @bind(ENV',CPR)  ~> K)) callStack(ENV :: RESTENV)  
   = (k(@args(ARGS) ~> @bind(insert(I1, ENV[I2], ENV'),CPR)  ~> K)) callStack(ENV :: RESTENV)   .

  eq (k(@args((INOUTPAR(I1) <:= InsLoc(INSLOC) . I2) , ARGS) ~> @bind(ENV,CPR)  ~> K))   inss(INSS)
   = (k(@args(ARGS) ~> @bind(insert(I1, getInsEnv(INSLOC,INSS)[I2], ENV),CPR)  ~> K))   inss(INSS) .

 crl [arrayArgBind] : 
     (k(@args((INOUTPAR(I1) <:= arrayLoc(ARRAYENV)[V2]) , ARGS) ~> @bind(ENV,CPR)  ~> K))   constraints(CONST) 
  => (k(@args(ARGS) ~> @bind(insert(I1, LOCATION, ENV),CPR)  ~> K))   constraints(simplify(CONST AND CONST')) 
  if arrayLookup(ARRAYENV, V2, CONST) => {LOCATION, CONST'} .

  --- context switching
  eq k(@args(nilPExpList) ~> @bind(ENV,CPR) ~> K) callStack(RESTENV)  
   = k(K) callStack(ENV :: RESTENV)   .

  rl [return] : (k(@pop ~> K)) callStack(ENV :: RESTENV)   
  => (k(K)) callStack(RESTENV)    .

  eq (k(return ~> @pop ~> K)) callStack(ENV :: RESTENV)  
   = (k(K)) callStack(RESTENV)   . 

  eq (k(return ~> S ~> K))  
   = (k(return ~> K))  . 
endm

--- starting new cycle
mod CYCLE-SEMANTICS is
  inc CALL-SEMANTICS .

  op @end : -> K [ctor] .
  op timerUpdate : Rat KConfig ~> KConfig .

  vars REST RESTINS RESTINSS KCF : KConfig .
  var PNAME : Pid .
  var PARAM : VarSecTypePairList .
  var S : Stmt . 
  var CT : Nat .
  var ENV : Env .
  var PRID : ProcessId .  
  var TNAT : SemanticValue .
  vars PL PL2 : List{ProcessId} .

  eq timerUpdate(CT, empty) = empty .
  eq timerUpdate(CT, ins(timer(@timer(TNAT)) KCF) RESTINS) 
  = ins(timer(@timer(simplify(TNAT + syn2sem(T# CT ms)))) KCF) timerUpdate(CT, RESTINS) .

  eq [prCall] :
     k(.K)
     callStack(PS:ProgramStack)
     pList(PRID :: PL)
     done(PL2)
     inss(ins(id(PRID) fbtype(PNAME) mem(ENV) RESTINS) RESTINSS)
     PARDef(PNAME, PARAM, S)
   = k(S)
     callStack(ENV)
     pList(PL)
     done(PL2 :: PRID)
     inss(ins(id(PRID) fbtype(PNAME) mem(ENV) RESTINS) RESTINSS)
     PARDef(PNAME, PARAM, S) .

  eq [scExec] :
     k(@end) 
     inss(RESTINSS)
     cycleTime(CT)
     pList(nilProcIds)
     done(PL) 
   = k(.K)  
     inss(timerUpdate(CT, RESTINSS))
     cycleTime(CT)
     pList(PL)
     done(nilProcIds) .
endm

--- semantics for time-related FBs (TON, ...)
fmod TIMER-SEMANTICS is 
  inc CODE-CONFIG .

  vars CPR : ProcessId .
  var IDSTACK : List{ProcessId} .
  var K : K .  
  vars CT PTIME : Rat .
  var PV1 : PValue .
  vars RESTINSS : KConfig .
  vars REST REST2 : KConfig .
  var TVAL : SemanticValue .

---(
  eq id(CPR) (k(@#timerValue ~> K))  
     inss(ins(id(CPR) timer(@timer(TVAL)) REST2) RESTINSS)
   =
     id(CPR) (k(TVAL ~> K))  
     inss(ins(id(CPR) timer(@timer(TVAL)) REST2) RESTINSS) .

  eq id(CPR) k(@#timerSetValue(T# PTIME ms) ~> K)  
     inss(ins(id(CPR) timer(@timer(TVAL)) REST2) RESTINSS)
   =
     id(CPR) k(K)  
     inss(ins(id(CPR) timer(@timer(syn2sem(T# PTIME ms))) REST2) RESTINSS) .
---)
endfm

