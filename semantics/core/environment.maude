view Oid from TRIV to CONFIGURATION is
  sort Elt to Oid .
endv


mod NECONFIGURATION is
  including CONFIGURATION .

  sort NEConfiguration .
  subsort NEConfiguration < Configuration .
  subsort Object < NEConfiguration .

  op __ : NEConfiguration Configuration -> NEConfiguration [ctor ditto] .
  op __ : Configuration NEConfiguration -> NEConfiguration [ctor ditto] .

  sort World .
  op {_} : Configuration -> World [ctor] .
endm

view Msg from TRIV to CONFIGURATION is
  sort Elt to Msg .
endv

mod OBJ-STRUCTURE is
  including CODE-CONFIG .
  including NECONFIGURATION .
  including MAP{PExp, Nat} * (op undefined to undefinedTimer) .
  including MAP{PExp, Bool} * (op undefined to undefinedDisable) .
  including DYNAMICS-EVALUATION .

  subsort PExp < Oid .

  sort PLCMachine .
  subsort PLCMachine < NEConfiguration .

  subsort PLCMachine < Cid .
  op PLCMachine : -> PLCMachine [ctor format(! o)] .
  op proc   :_ : KConfig -> Attribute [ctor gather(&) format(nm o d d)] .
  op state :_ : Map{PExp, SemanticValue} -> Attribute [ctor gather(&) format(nm o d d)] .
  op timer :_ : Nat -> Attribute [ctor gather(&) format(nm o d d)] .
  op actList :_ : List{PExp} -> Attribute [ctor gather(&) format(nm o d d)] .
  op senList :_ : List{PExp} -> Attribute [ctor gather(&) format(nm o d d)] .
  op flow :_ : Dynamics -> Attribute [ctor gather(&) format(nm o d d)] .
  op gen :_ : Nat -> Attribute [ctor gather(&) format(nm o d d)] .
endm

mod UTIL is
  including DYNAMICS-EVALUATION .
  including OBJ-STRUCTURE .

  op mte : NEConfiguration ~> Nat .
  op monus : Nat Nat ~> Nat .
  op enable : NEConfiguration ~> NEConfiguration .

  var PLC : Object .
  var CONF : NEConfiguration .
  var ATTRS : AttributeSet .
  var TIMER : Nat .
  var DISABLED : Map{PExp, Bool} .
  var PNAME : Pid .
  vars T T' : Nat .
  var CONF' : Configuration .
 
  eq mte(PLC CONF) = min(mte(PLC), mte(CONF)) .
  eq mte(< O:Oid : PLCMachine | timer : TIMER, ATTRS >)
   = TIMER .

  eq monus(T, T') = T - T' .
endm

mod TIMEEFFECT is
  including NECONFIGURATION .
  including UTIL .


  op timeEffect : Configuration Nat ~> Configuration .

  var KC : KConfig .
  var ATTRS : AttributeSet .
  vars STATE STATE' : Map{PExp, SemanticValue} .
  var FLOW : Dynamics .
  vars T MINT MAXT : Nat .
  var TIMER : Nat .
  var CC : SemanticValue .
  var CONF : NEConfiguration .
  var CONF' : Configuration .

  eq timeEffect(none, T) = none .
  eq timeEffect(
     < O:Oid : PLCMachine | timer : TIMER, 
                            flow : FLOW,
                            state : STATE, ATTRS >, T)
   = < O:Oid : PLCMachine | timer : monus(TIMER, T),
                            flow : FLOW,
                            state : timeEffect(FLOW, STATE, T),
                            ATTRS > .
  
  eq timeEffect(CONF CONF', T) = timeEffect(CONF, T) timeEffect(CONF', T) .
endm 

mod EXECUTE is
  including TIMEEFFECT .
  including CODE-CONFIG .
  including BOOLEAN-EXPR .

  ops sense actuate : Configuration ~> Configuration .
  op actuate : Map{PExp, SemanticValue} KConfig List{PExp} ~> Map{PExp, SemanticValue} .
  op sense : KConfig Map{PExp, SemanticValue} List{PExp} ~> KConfig .
  op isAllReadyFrozen : Configuration ~> Bool . 

  var CONF : Configuration .
  vars T : Nat .
  var IM : Map{ProcessId, Nat} .
  var TIMER : Nat .
  vars X PNAME : Pid .
  var PRID : ProcessId .
  vars KC RESTI RESTT INSS : KConfig .
  var ATTRS : AttributeSet .
  var K : K .
  vars SENLIST ACTLIST : List{PExp} .
  var STATE : Map{PExp, SemanticValue} .
  vars V V' : SemanticValue .
  var MEM : Env .
  var STORE : Map{Location, SemanticValue} .
  var L : Location .
  var ENV : Env .
  var STACK : ProgramStack .
  var N : Nat .

--- string conversion
  eq task(id(PRID) k(thisBlock() ~> K) callStack(ENV :: STACK) RESTT)
     inss(ins(mem(ENV) iname(PNAME) RESTI) INSS)
   = task(id(PRID) k(PNAME ~> K) callStack(ENV :: STACK) RESTT)
     inss(ins(mem(ENV) iname(PNAME) RESTI) INSS) .

  eq < O:Oid : PLCMachine | gen : N, proc : task(k(boolUserInput() ~> K) RESTT) KC >
   = < O:Oid : PLCMachine | gen : s N, proc : task(k(b(smtv(O, N)) ~> K) RESTTT) KC > .

 crl [envTick] :
     {CONF} => {timeEffect(CONF, T)}
  if T := mte(CONF) /\ T > 0 .

 crl [start] :
     {CONF} => {enable(sense(actuate(CONF)))}
  if isAllReadyFrozen(CONF) .

  eq isAllReadyFrozen(
     < O:Oid : PLCMachine | timer : 0, proc : [KC], ATTRS > CONF)
   = isAllReadyFrozen(CONF) .

  eq isAllReadyFrozen(
     < O:Oid : PLCMachine | timer : 0, proc : KC, ATTRS > CONF)
   = false .

  eq isAllReadyFrozen(CONF) = true [owise] .

  eq actuate(
     < O:Oid : PLCMachine | timer : 0,
                            proc : [task(id(PRID) RESTT)]
                                   inss(ins(id(PRID) fbtype(PNAME) RESTI) INSS) KC,
                            state : STATE,
                            actList : ACTLIST,
                            ATTRS > CONF) 
   = < O:Oid : PLCMachine | timer : 0,
                            proc : [task(id(PRID) RESTT)]
                                   inss(ins(id(PRID) fbtype(PNAME) RESTI) INSS) KC,
                            state : actuate(STATE, task(id(PRID) RESTT)
                                    inss(ins(id(PRID) fbtype(PNAME) RESTI) INSS) KC, 
                                    ACTLIST),
                            actList : ACTLIST,
                            ATTRS > actuate(CONF) .
  eq sense(
     < O:Oid : PLCMachine | timer : 0,
                            proc : [task(id(PRID) RESTT)]
                                   inss(ins(id(PRID) fbtype(PNAME) RESTI) INSS) KC,
                            state : STATE,
                            senList : SENLIST,
                            ATTRS > CONF) 
   = < O:Oid : PLCMachine | timer : 0,
                            proc : [sense(task(id(PRID) RESTT)
                                         inss(ins(id(PRID) fbtype(PNAME) RESTI) INSS) KC,
                                         STATE, SENLIST)],
                            state : STATE,
                            senList : SENLIST,
                            ATTRS > sense(CONF) .

  eq enable(
     < O:Oid : PLCMachine | timer : 0, proc : [task(RESTT)] cycleTime(T) KC, ATTRS > CONF)
   = < O:Oid : PLCMachine | timer : T, proc : task(RESTT) cycleTime(T) KC, ATTRS >
     enable(CONF) .

  eq actuate(CONF) = CONF [owise] .
  eq sense(CONF) = CONF [owise] .
  eq enable(CONF) = CONF [owise] .

  eq actuate(((X ||-> V), STATE), 
             store(((L |-> V'), STORE))
             inss(ins(id(PRID) fbtype(PNAME) mem(((X |-> L), MEM)) RESTI) INSS) KC,
             (X, ACTLIST))
   = ((X ||-> V'), actuate(STATE, store(((L |-> V'), STORE))
             inss(ins(id(PRID) fbtype(PNAME) mem(((X |-> L), MEM)) RESTI) INSS) KC,
             ACTLIST)) .

  eq actuate(STATE, KC, nilPExpList) = STATE .

  eq sense(store(((L |-> V'), STORE))
           inss(ins(id(PRID) fbtype(PNAME) mem(((X |-> L), MEM)) RESTI) INSS) KC,
           ((X ||-> V), STATE), (X, SENLIST))
   =  sense(store(((L |-> V), STORE))
           inss(ins(id(PRID) fbtype(PNAME) mem(((X |-> L), MEM)) RESTI) INSS) KC,
           ((X ||-> V), STATE), SENLIST) .

  eq sense(KC, STATE, nilPExpList) = KC .
endm

mod COMMUNICATION is
  including UTIL .

  sort Channel .
  subsort Channel < NEConfiguration .
  subsort Channel < Cid .
  sorts CMsg CMsgs .
  subsort CMsg < CMsgs .

  op Channel : -> Channel [ctor] .
  op Channel : -> Cid .
  op link : Oid Oid -> Oid [ctor comm] .
  op mwrapper : Oid Oid PExp PExp PExp -> CMsg [ctor] .
  op emptyBuffer : -> CMsgs [ctor] .
  op __ : CMsgs CMsgs -> CMsgs [ctor assoc comm id: emptyBuffer] .
  op insert : CMsgs CMsg ~> CMsgs .
  op checkMsg : CMsgs Oid PExp PExp ~> Bool .
  op validity :_ : Bool -> Attribute [ctor] .
  op buffer :_ : CMsgs -> Attribute [ctor] .

  vars O O' : PExp .
  var K : K .
  vars RESTT KC : KConfig .
  vars ATTRS ATTRS' : AttributeSet .
  var BUFFER : CMsgs .
  var NEWM : CMsg .
  vars DATA DATA' SFBID RFBID : PExp .
  var B : Bool .

  eq insert(BUFFER mwrapper(O, O', SFBID, RFBID, DATA'), mwrapper(O, O', SFBID, RFBID, DATA))
   = BUFFER mwrapper(O, O', SFBID, RFBID, DATA) .
  eq insert(BUFFER, NEWM) = BUFFER NEWM [owise] .
  eq checkMsg(BUFFER mwrapper(O, O', SFBID, RFBID, DATA), O, SFBID, RFBID) = true .
  eq checkMsg(BUFFER, O, SFBID, RFBID) = false [owise] .

  rl [con-succ] :
     < O : PLCMachine | proc : task(k(connectRequest(O') ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, ATTRS' >
  => < O : PLCMachine | proc : task(k(K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, ATTRS' > .

  rl [con-back] :
     < O : PLCMachine | proc : task(k(connectRequest(O') ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, ATTRS' >
  => < O : PLCMachine | proc : task(k(K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, ATTRS' > .

---(
  rl [con-fail] : --- failure, denial, abort 
     < O : PLCMachine | proc : task(k(connectRequest(O') ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, ATTRS' >
  => < O : PLCMachine | proc : task(k(K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, ATTRS' > .
---)

  rl [discon-vol]:
     < O : PLCMachine | proc : task(k(disconnect(O') ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, ATTRS' >
  => < O : PLCMachine | proc : task(k(K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, ATTRS' > .

  rl [isConnected]:
     < O : PLCMachine | proc : task(k(isConnected(O') ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : B, ATTRS' >
  => < O : PLCMachine | proc : task(k(if B then TRUE else FALSE fi ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : B, ATTRS' > .

---(
  rl [discon-unvol] :
     < link(O, O') : Channel | validity : true, ATTRS' >
  => < link(O, O') : Channel | validity : false, ATTRS' > .
---)
  rl [sendData] :
     < O : PLCMachine | proc : task(k(sendData(O', SFBID, RFBID, DATA) ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : BUFFER, ATTRS' >
  => < O : PLCMachine | proc : task(k(TRUE ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : insert(BUFFER, mwrapper(O, O', SFBID, RFBID, DATA)), ATTRS' > .

  rl [sendData-fail] :
     < O : PLCMachine | proc : task(k(sendData(O', SFBID, RFBID, DATA) ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, buffer : BUFFER, ATTRS' >
  => < O : PLCMachine | proc : task(k(FALSE ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, buffer : BUFFER, ATTRS' > .

  rl [rcvData] :
     < O : PLCMachine | proc : task(k(rcvData(O', SFBID, RFBID) ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : (BUFFER mwrapper(O', O, SFBID, RFBID, DATA)), ATTRS' >
  => < O : PLCMachine | proc : task(k(DATA ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : BUFFER, ATTRS' > .

  rl [rcv-fail] :
     < O : PLCMachine | proc : task(k(rcvData(O', SFBID, RFBID) ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, buffer : BUFFER, ATTRS' >
  => < O : PLCMachine | proc : task(k(rcvError ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : false, buffer : BUFFER, ATTRS' > .

 crl [rcv-noMsg] :
     < O : PLCMachine | proc : task(k(rcvData(O', SFBID, RFBID) ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : BUFFER, ATTRS' >
  => < O : PLCMachine | proc : task(k(rcvError ~> K) RESTT) KC, ATTRS >
     < link(O, O') : Channel | validity : true, buffer : BUFFER, ATTRS' > 
  if not checkMsg(BUFFER, O', SFBID, RFBID) .
endm

