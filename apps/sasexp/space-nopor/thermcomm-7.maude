load /home/jlee/plc-release/scripts/../semantics/interpreter/environment 
load /home/jlee/plc-release/scripts/../libs/libcollection.maude
 

mod APP is 
inc PLC-CORE .
inc PLC-LIB .
ops projectFile1 projectFile2 : -> StratonProject .
eq projectFile1 =
(
BEGINCONFIG
PROGRAMS
'Thermostat ;; 
PROGRAMSEND
SUBPROGRAMS
emptyProgramList
SUBPROGRAMSEND

TYPE
emptyTypeDecl
ENDTYPE



BEGINVAR
emptyVarDecl
ENDVAR


CycleTime: # 3
ENDCONFIG

BEGINPOUS
PROGRAM 'Thermostat

BEGINVAR
't2 : DINT : ##PRIV ;
'switch : DINT : ##OUT ;
'comm : fbtype [ 'CONNECT ] : ##PRIV ; 
'rcv : fbtype [ 'URCV ] : ##PRIV ; 
'send : fbtype [ 'USEND ] : ##PRIV ; 
't1 : DINT : ##IN ;
ENDVAR


CODE
'comm ( TRUE , # "plc2" ) ; 
IF NOT ('comm . 'VALID) THEN 
return ; 
ENDIF ;

'rcv(TRUE, # "plc2", # "send") ;
@assertTime(# 1, # 2) ;
'send( TRUE, # "plc2" , # "rcv" , 't1) ;
@assertTime(# 1, # 2) ;
't2 := 'rcv . 'DATA ;

IF 't1 + 't2 < # 15 THEN
  'switch := # 0 ;
ELSE
  IF 't1 > 't2 THEN
    'switch := # 0 ;
  ELSE
    'switch := # 1 ;
  ENDIF ;
ENDIF ;

ENDCODE
ENDPROGRAM
ENDPOUS
) .



eq projectFile2 =
(
BEGINCONFIG
PROGRAMS
'Thermostat ;; 
PROGRAMSEND
SUBPROGRAMS
emptyProgramList
SUBPROGRAMSEND

TYPE
emptyTypeDecl
ENDTYPE



BEGINVAR
emptyVarDecl
ENDVAR


CycleTime: # 3
ENDCONFIG

BEGINPOUS
PROGRAM 'Thermostat 
BEGINVAR
't1 : DINT : ##PRIV ;
'switch : DINT : ##OUT ;
'comm : fbtype [ 'CONNECT ] : ##PRIV ; 
'rcv : fbtype [ 'URCV ] : ##PRIV ; 
'send : fbtype [ 'USEND ] : ##PRIV ; 
't2 : DINT : ##IN ;
ENDVAR

CODE
'comm ( TRUE , # "plc1" ) ; 
IF NOT ('comm . 'VALID) THEN 
return ; 
ENDIF ;

'send( TRUE, # "plc1" , # "rcv" , 't1) ;
@assertTime(# 0, # 0) ;
'rcv(TRUE, # "plc1", # "send") ;
@assertTime(# 3, # 3) ;
't1 := 'rcv . 'DATA ;

IF 't1 + 't2 < # 15 THEN
  'switch := # 0 ;
ELSE
  IF 't1 > 't2 THEN
    'switch := # 1 ;
  ELSE
    'switch := # 0 ;
  ENDIF ;
ENDIF ;
ENDCODE
ENDPROGRAM
ENDPOUS

) .
  ops linkedFile1 linkedFile2 : -> StratonProject .
  eq linkedFile1 = projectFile1 <-load- lib .
  eq linkedFile2 = projectFile2 <-load- lib .
  ops app1 app2 : -> KConfig .
  eq app1 = [[[ linkedFile1 ]]] .
  eq app2 = [[[ linkedFile2 ]]] .

endm

mod MC is
  inc APP .
  inc COMMUNICATION . 

  op t : -> Var [ctor] .
  op init : ~> World .
  eq minDelay = [(1/1).Real] .

  eq init =
{
< # "plc1" : PLCMachine | proc : app1, timer : [(0).Integer], envTimer : 0, gen : 0, clock : 0, 
                          senList : ('t1), 
                          actList : ('switch),
                          state : (('t1 ||-> [(15).NzNat]), ('switch ||-> [(0).Zero])),
                          flow : ('t1(t) = s['t1] + ((s['switch] + c[[(-1).Int / (10).Int]]) * v[t])) >
< # "plc2" : PLCMachine | proc : app2, timer : [(0).Integer], envTimer : 0, gen : 0, clock : 0,
                          senList : ('t2), 
                          actList : ('switch),
                          state : (('t2 ||-> [(16).NzNat]), ('switch ||-> [(0).Zero])),
                          flow : ('t2(t) = s['t2] + ((s['switch] + c[[(-1).Int / (10).Int]]) * v[t])) >
< link(# "plc1", # "plc2") : Channel | validity : false, buffer : emptyBuffer >
  cconst([(true).Boolean]) tconst([(true).Boolean]) tickTurn((false).Bool) tgen((0).Nat)} .
  op X_X : Configuration -> World [ctor frozen] .
 ceq { < O:Oid : PLCMachine |  ATTRS:AttributeSet, clock : T:Nat > REST:Configuration }
   = X < O:Oid : PLCMachine |  ATTRS:AttributeSet, clock : T:Nat > REST:Configuration X
  if float(T:Nat) >= 21.0 .
endm 

smod POR is
  inc MC .

  strat internal @ Configuration .
  strat commu @ Configuration .
  strat allRules @ Configuration .
  strat porStep @ Configuration .
  strat indiv : Oid @ Configuration .

  sd internal := arrayLookup | array | iteTrue | iteFalse | IFTRUE | IFFALSE | IFFALSE-ELSIF | IFFALSE-ELSE | caseMatch | caseMatchRanged | caseNotMatch | caseNotMatchRanged | arrayArgBind .
  sd commu := discon-vol | sendData | rcvData | rcv-fail | rcv-noMsg .
  sd allRules := internal | commu | tick | start .

  var I : Oid .
  sd indiv(I) := matchrew {< I : PLCMachine | proc : P:KConfig, ATTRS:AttributeSet > CONF:Configuration} by P:KConfig using internal .

  sd porStep := (start ? idle : ((indiv(# "plc1") ? idle : indiv(# "plc2")) ? idle : (commu ? idle : tick))) .

  strat goalState @ Configuration .
  sd goalState := match {< # "plc1" : PLCMachine | state : (('t1 ||-> SM:SemanticValue), REST:Map{PExp, SemanticValue}), ATTRS:AttributeSet > < # "plc2" : PLCMachine | state : (('t2 ||-> SM2:SemanticValue), REST2:Map{PExp, SemanticValue}), ATTRS2:AttributeSet > CONF:Configuration}
                  s.t. checkSAT(SM:SemanticValue > [(30).NzNat]
                   OR SM2:SemanticValue > [(30).NzNat]) .

  strat porStar @ Configuration .
  sd porStar := (porStep *) .

  strat noPor @ Configuration .
  sd noPor := (allRules *) .
endsm

srew init using (allRules *) .


eof
search [1]  init
=>*
{< # "plc1" : PLCMachine | state : (('t1 ||-> SM:SemanticValue), REST1:Map{PExp, SemanticValue}), ATTRS1:AttributeSet >
 < # "plc2" : PLCMachine | state : (('t2 ||-> SM2:SemanticValue), REST2:Map{PExp, SemanticValue}), ATTRS2:AttributeSet > R:Configuration } 
such that checkSAT(SM:SemanticValue < [(14).Nat] OR SM2:SemanticValue < [(14).Nat]) .

